.# Shell-like commands for Aya

import ::sys
import ::io
import ::terminal

struct ::shellresult [::value]

def shellresult::__repr__ {self,
    self.value
}

def shellresult::lines {self,
    self.value "\n" |
}


exportall


.#? ls\n  print a list of files in the working dir
def ::ls {
    ""2MZ4MZ
    .E 0 = {
        ; "  <no files>"
    } {
        C "\n" *
    } .?

    shellresult!
}

def ::grep_impl {res::shellresult rgx::str,
    res.lines {rgx &E0>} I
    "\n" * shellresult!
}

def ::grep {`(grep_impl)}


.#? pwd\n  print working directory
def ::pwd {path.working:P}


.#? cd dir\n  change directory
def ::cd {`(sys.cd)}


.#? mkdir S\n  create a new directory
def ::mkdir {`(sys.mkdir)}


.#? cat S\n  print a file to the console
def ::cat {`({filename::str,
    filename G println
})}


def ::echo {`:P}


def ::touch {`({s::str,"" s 1.G})}


def ::rm {`(6MZ)}


def ::run {`(".aya"+G~)}

def ::clear {:terminal^, terminal.clear}


def (__aya__.interpreter)::shell {str : cmd args,
    str ' N $ :1 = {
        ;; str~
    } {
        | trim :args; :cmd;
        cmd~ args
    } .?
}
