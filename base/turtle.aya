    .# This file is a part of Aya: https://github.com/nick-paul/aya-lang

.{
    turtle

    A simple turtle graphics library
    The API is modeled after python's built-in turtle library

    Examples:
        .# Offset nested balls
        {, 200:width 200:height} turtle!:t
        10R :# {n, {0.1 t.right n t.fd 2:Z} 100 %} ;

        .# A small colorful star
        colors.red {10\.hueshift $ t.pencolor 40 t.fd 124 t.right} 36 %
.}

import ::canvas
import ::color

class ::turtle

impl turtle::__init__ {params::dict self,

    .# Default values
    {,
        0:x
        0:y
        0:r
        400:width;
        300:height;
        ::deg:units;
        1 :autodraw;
    }
    .# Merge user defined
    params .+ :params;

    params.x self.:_x;
    params.y self.:_y;
    params.r self.:_r;

    params.units `in [::deg ::degrees] self.:_use_deg;

    params.width self.:_w;
    params.height self.:_h;
    {, self._w:width self._h:height} canvas! self.:_cvs;

    1 self.:_pen_down
    params.autodraw self.:_autodraw;
}

impl turtle::__repr__ {self,
    "turtle: pos=($(self._x), $(self._y)) dir=$(self._r)"
}

.#################
.# Turtle Motion #
.#################

impl turtle::fd {n self : px(self._x) py(self._y),
    self._x self._r Mc n * + self.:_x;
    self._y self._r Ms n * + self.:_y;
    px py self._step_from
}

impl turtle::right {amount self,
    self._r amount self._to_rad + self.:_r;
}

impl turtle::left {amount self,
    self._r amount self._to_rad - self.:_r;
}

impl turtle::goto {x y self : px(self._x) py(self._y),
    x self.:_x;
    y self.:_y;
    px py self._step_from
}

impl turtle::pos {self,
    self._x self._y
}

impl turtle::draw {self,
    self._cvs.show
}

.###############
.# Pen Control #
.###############

impl turtle::pendown {self,
    1 self.:_pen_down
}

impl turtle::penup {self,
    0 self.:_pen_up
}

impl turtle::isdown {self,
    self._pen_down
}

impl turtle::pencolor {c::color self,
    c self._cvs.set_color
}


.####################
.# Helper Functions #
.####################


.# This function maps the turtle
.# coordinate space to the canvas coordinate space
.# The turtle lives in a coordinate frame where (0,0) is
.# at the center of the canvas.
impl turtle::_tf {x y self,
    x self._w 2/ +
    y self._h 2/ +
}

.# Convert the input value to radians if units is degrees
impl turtle::_to_rad {val self,
    self._use_deg
        val :1p1 180 / *
        val
    .?
}


.# Callback function for after the turtle steps
.# Assumes the turtle's position has already been updated
.# px and py are the turtles previous location
impl turtle::_step_from {px py self,
    self._pen_down {
        px py self._tf
        self._x self._y self._tf
        self._cvs.line
        self._autodraw {
            self._cvs.show
        } ?
    } ?
}

.#############
.# Overloads #
.#############

impl turtle::__rpow__ {self val,
  val self.fd
  self
}


.# For testing
{,
    100 :width
    100 :height
    ::deg :units
} turtle! :t
