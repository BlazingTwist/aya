module ::csv

def csv::parse {dlm,
    "\n"| :# {dlm | :#{.rmquote.!}}
}

def csv::open {f : csv^,
    fP G "," csv.parse
}

def csv::read {kwargs : csv^
                        clabel(1)
                        rlabel(0)
                        filename(nil)
                        csv_str(nil) dlm(",")
                        data
                        colnames(nil)
                        rownames(nil),
    kwargs .W

    (csv_str nil = filename nil = &) {"csv.read: Must provide either filename or csv_str" .D} ?

    .# Open the file and read into data
    filename nil =! {filenameP G :csv_str;} ?

    csv_str dlm csv.parse :data;

    .# CSV has column headers
    clabel {
        data V :colnames; ;
        .# If there are row labels, the first entry is for the index colum
        rlabel {colnamesV;:colnames;} ?
    } ?

    rlabel {
        data :#{V\;} :rownames;
    } ?

    {,
        data:data
        colnames:colnames
        rownames:rownames
    }
}
