import ::color

class ::canvas

def canvas::__init__ ({params::dict self ,

    .# Default values
    {,
        400 :width
        400 :height
        "Canvas" :name
        0 :autoflush
        0 :show
        colors.white :bg
        colors.black :fg
        params W
    }:params;

    params.width self.:width;
    params.height self.:height;

    ::new params 0 MG self.:id ;
}.capture[::colors])

def canvas::__str__ {self,
    "<canvas($(self.id))>"
}

(canvas.__str__.`,0); canvas.:__repr__;

def canvas::delay {self,
    self._delay_ms 0 > {self._delay_ms$:P :Z} ?
}

def canvas::line {xa ya xb yb self,
    ::line {, xa:xa ya:ya xb:xb yb:yb } self.id MG ;
}

def canvas::point {x y self,
    ::line {, x:xa:xb y:ya:yb} self.id MG ;
}

def canvas::circle {x y r self,
    ::ellipse {, x:x y:y r2*:w:h } self.id MG ;
}

def canvas::fillcircle {x y r self,
    ::ellipse {, 1:fill x:x y:y r2*:w:h } self.id MG ;
}

def canvas::set_color {color self,
    ::set_color {, color.r:r color.g:g color.b:b } self.id MG ;
}

def canvas::set_alpha {a self,
    ::set_alpha {, a:a } self.id MG ;
}

def canvas::set_grad {start ca end cb cycle self,
    ::set_paint {, start.[0]:xa start.[1]:ya ca:start
                   end.[0]:xb   end.[1]:yb   cb:end
                   cycle:cycle ::grad:type } self.id MG ;
}

def canvas::set_bg {color self,
    ::set_bg {, color.r:r color.g:g color.b:b } self.id MG ;
}

def canvas::save {filename self,
    ::save {,
        .# Convert to string (in case it is a path object)
        filename P
        .# Add file extension if it does not exist
        ".png" `in filename ! {".png"+} ?
        .# use as path
        :file
    } self.id MG ;
}

def canvas::close {self,
    ::close {,} self.id MG ;
}

def canvas::show {self,
    ::show {,} self.id MG ;
}

def canvas::isopen {self,
    ::isopen {,} self.id MG
}

def canvas::rect {x y w h self,

    ::rect {, x:x y:y w:w h:h } self.id MG ;
}

def canvas::fillrect {x y w h self,
    ::rect {, x:x y:y w:w h:h 1:fill} self.id MG ;
}

def canvas::clear {self,
    ::clear {,} self.id MG ;
}
