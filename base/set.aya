.# This file is a part of Aya: https://github.com/nick-paul/aya-lang

.# set.aya
.# Defines the set data structure

.{? type: set
    The set type is a simple list type where there exits no duplicate elements.
    Create a set by entering a list followed by the set constructor
.}


class ::set

impl set::__init__ {l::list self,
    l self.:list ;
}


.#
.# Adding / Removing elements
.#

.#? ::any ::set.__add__\n  add an item to a set
impl set::__add__ {item self,
    item self.in ! {self.list [item] K;} ? self
}

.#? ::any ::set.__sub__\n  remove an item from a set
impl set::__sub__ {item self,
    self.list [item] :| self.:list
}



.#
.# Set operations
.#

.#? ::set ::set.__or__\n  set union
impl set::__or__ {other::set self,
    other.list $ \; self.list K :~ self.M!
}

.#? ::set ::set.__and__\n  set intersection
impl set::__and__ {other::set self : l,
    self.list :l;
    other.list # {l \N$ 0:> {I} {;;} .?} self.M!
}


.#? ::set ::set.__div__\n  set difference
impl set::__div__ {other::set self,
    other.list $\; self.list :| self.M!
}


.#? ::set ::set.__eq__\n test if two sets are the same
impl set::__eq__ {other::set self,
    other.list self.list =
}


.#? ::set ::set.__lt__\n  proper subset
impl set::__lt__ {other::set self,
    other self.__leq__            .# subset
    other.list self.list = !  .# proper subset
    &
}

.#? ::set ::set.__gt__\n  proper subset
impl set::__gt__ {\.__lt__}


.#? ::set ::set.__leq__\n subset
impl set::__leq__ {other::set self : l,
    other.list :l;
    self.list # { l \ N \; 0 :>} {*}F
}

.#? ::set ::set.__geq__\n subset
impl set::__geq__ {\.__leq__}



.#
.# Other functions
.#

.#? ::any ::set.in\n  tests if an item is in a set
impl set::in {item self,
    self.list item N\; :1 =!
}


.#? ::set.__len__\n  returns the number of elements in the set
impl set::__len__ { .list E }


.#? ::set.clear\n  clear the contents of a set
impl set::clear {[]\ .:list}

impl set::__repr__ {.listP"s"\+}
impl set::__str__  {.listP"s"\+}

impl set::__each__ {blk self,
    self.list :# {blk}
}


{`(set!)}:s;


.#########
.# TESTS #
.#########

import ::test

`~ {:ts,

    "set" testset! :ts;

    {s[1 2 3].list} [[1 2 3]] ts+
    {3 s[1 2 3] -} [s[1 2]] ts+
    {4 s[1 2 3] -} [s[1 2 3]] ts+
    {4 s[1 2 3] +} [s[1 2 3 4]] ts+
    {3 s[1 2 3] +} [s[1 2 3]] ts+
    {s[1 2 3] s[2 3 4] |} [s[1 2 3 4]] ts+
    {s[1 2 3] s[2 3 4] &} [s[2 3]] ts+
    {s[1 2 3] s[1 4] /} [s[2 3]] ts+
    {s[1 2 3] s[1 2 3] =} [1] ts+
    {s[1 2 3] s[2 3] <} [1] ts+
    {s[1 2 3] s[1 2 3] <} [0] ts+
    {s[1 2 3] s[1 2 3] :<} [1] ts+
    {s[2 3] s[1 2 3] >} [1] ts+
    {s[1 2 3] s[1 2 3] >} [0] ts+
    {s[1 2 3] s[1 2 3] :>} [1] ts+
    {2 s[1 2 3].in} [1] ts+
    {0 s[1 2 3].in} [0] ts+
    {s[1 2 3] E} [3] ts+
    {s[1 2 3].clear.list} [[]] ts+


    ts __aya__.addtest
}
