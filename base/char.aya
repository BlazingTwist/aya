.# This file is a part of Aya: https://github.com/nick-paul/aya-lang

.# char.aya
.# Defines functions for working with characters

{,
    .#? C.islower\n  tests if a character is lowercase
    {$ 'a :> \ 'z :< &}:islower;

    .#? C.isupper\n  tests if a character is uppercase
    {$ 'A :> \ 'Z :< &}:isupper;

    .#? C.toupper\n  converts a character to uppercase
    {$.isupper! {!} ? }:upper;

    .#? C.tolower\n  converts a character to lowercase
    {$.islower! {!} ? }:lower;

    .#? C.isdigit\n  tests if a character is a digit
    {$ '0 :> \ '9 :< &}:isdigit;

    .#? C.iswhitespace\n  tests if a character is a whitespace character
    {P "\W+" & E 1 =}:iswhitespace;

    .#? C.isascii\n  test if character is ascii
    {:' 127 <}:isascii;

    .#? C.isalpha\n  test if character is alphabetical
    {$ $ 'a :> \ 'z :< & \ $ 'A :> \ 'Z :< & |}:isalpha;

    {s c,
        s cP | :1| \:#{c+}\J
    }:split;
}:char;

.#? S spechar\n  converts the name of a special character into the corresponding special character
{a::str,"\"\\{$a}\""~.'}:spechar;



.#########
.# TESTS #
.#########

import ::test

`~ {:ts,

    "char" testset! :ts;

    {"cC*" #.islower} [[1 0 0]] ts+
    {"cC*" #.isupper} [[0 1 0]] ts+
    {"HelLo!" #.upper} ["HELLO!"] ts+
    {"HelLo!" #.lower} ["hello!"] ts+
    {"Cr4zy1!" #.isdigit} [[0 0 1 0 0 1 0]] ts+
    {"\n\t s" #.iswhitespace} [[1 1 1 0]] ts+
    {"alpha" spechar '\alpha' =} [1] ts+

    ts __aya__.addtest

}
